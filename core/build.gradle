plugins {
	id("com.github.johnrengelman.shadow")
	id("io.micronaut.library")
	id("io.micronaut.aot")
	id("maven-publish")
	id("jacoco")
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	annotationProcessor("io.micronaut:micronaut-http-validation")
	annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
	annotationProcessor("io.micronaut.validation:micronaut-validation-processor")
	runtimeOnly("io.micronaut:micronaut-http-client")
	runtimeOnly("io.micronaut:micronaut-http-server-netty")
	runtimeOnly("io.micronaut.validation:micronaut-validation")


	// Reactor for reactive streams
	implementation("io.projectreactor:reactor-core:3.5.12")

	api("jakarta.annotation:jakarta.annotation-api")
	api("jakarta.validation:jakarta.validation-api")

	runtimeOnly("ch.qos.logback:logback-classic")

	// uses exileapi v1.0.1 - NOTE: you must use api to propagate the proto files to the other projects
	api("build.buf.gen:tcn_exileapi_grpc_java:${exileapiGrpcVersion}")

	implementation("com.zaxxer:HikariCP:6.3.0")

	// gRPC client dependencies only (no server)
	api("io.grpc:grpc-protobuf:${grpcVersion}")
	api("io.grpc:grpc-services:${grpcVersion}") {
		exclude group: "io.grpc", module: "grpc-health-service"
	}
	api("io.grpc:grpc-stub:${grpcVersion}")
	compileOnly("org.apache.tomcat:annotations-api:6.0.53")
	runtimeOnly("io.grpc:grpc-netty-shaded:${grpcVersion}")
	api("com.google.protobuf:protobuf-java-util:${protobufVersion}")

	// Add SnakeYAML for YAML configuration support
	runtimeOnly("org.yaml:snakeyaml")

	testRuntimeOnly("io.micronaut:micronaut-http-client")

	api('io.micronaut.serde:micronaut-serde-api:2.12.1')
	implementation('com.google.protobuf:protobuf-java:4.28.3')
	api('io.micronaut:micronaut-context:4.7.14')
	api('io.micronaut:micronaut-core:4.7.14')
	api('com.fasterxml.jackson.core:jackson-annotations:2.17.2')
	api("build.buf.gen:tcn_exileapi_protocolbuffers_java:${exileapiProtobufVersion}")
	api("io.grpc:grpc-api:${grpcVersion}")
}

java {
	sourceCompatibility = JavaVersion.toVersion("17")
	targetCompatibility = JavaVersion.toVersion("17")
}

graalvmNative.toolchainDetection = false

micronaut {
	runtime("netty")
	testRuntime("junit5")
	processing {
		incremental(true)
		annotations("com.tcn.exile.*")
	}
	aot {
		// Please review carefully the optimizations enabled below
		// Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
		optimizeServiceLoading = false
		convertYamlToJava = false
		precomputeOperations = true
		cacheEnvironment = true
		optimizeClassLoading = true
		deduceEnvironment = true
		optimizeNetty = true
		replaceLogbackXml = true
	}
}

// Configure JaCoCo for test coverage
jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/*$Lambda$*/**',
				'**/*$$*/**',
				'**/*_Micronaut*/**',
				'**/micronaut/**'
			])
		}))
	}
}

test {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
}
