plugins {
	id("com.github.johnrengelman.shadow") version "8.1.1" apply(false)
	id("io.micronaut.application") version "${micronautGradlePluginVersion}" apply(false)
	id("io.micronaut.aot") version  "${micronautGradlePluginVersion}" apply(false)
	id("io.micronaut.docker") version  "${micronautGradlePluginVersion}" apply(false)
	id("com.google.protobuf") version "0.9.4" apply(false)
	id("maven-publish")
	id("com.diffplug.spotless") version "7.0.3"
}

repositories {
	mavenCentral()
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'com.diffplug.spotless'

	repositories {
		mavenCentral()
		maven {
			name = 'buf'
			url 'https://buf.build/gen/maven'
		}
	}

	dependencies {
		implementation 'ch.qos.logback:logback-classic:1.5.17'
		implementation 'ch.qos.logback:logback-core:1.5.17'
		implementation 'org.slf4j:slf4j-api:2.0.17'

		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
		testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
		testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
		testImplementation 'org.mockito:mockito-core:4.0.0'
	}
	publishing {
		publications {
			maven(MavenPublication) {
				groupId = 'com.tcn.exile.sati'
				from components.java
			}
		}
		repositories {
			maven {
				name = "GitHubPackages"
				url = uri("https://maven.pkg.github.com/tcncloud/sati")
				credentials {
					username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
					password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
				}
			}
		}
	}
	jar {
		manifest {
			attributes 'Implementation-Version': project.version
		}
	}
	spotless {
		// optional: limit format enforcement to just the files changed by this feature branch
		ratchetFrom 'origin/main'

		format 'misc', {
			// define the files to apply `misc` to
			target '*.gradle', '.gitattributes', '.gitignore'

			trimTrailingWhitespace()
			leadingSpacesToTabs()
			endWithNewline()
		}
		java {
			googleJavaFormat()
			formatAnnotations()
			licenseHeader('''/*
 *  (C) 2017-$YEAR TCN Inc. All rights reserved.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */''')
		}
	}
}
